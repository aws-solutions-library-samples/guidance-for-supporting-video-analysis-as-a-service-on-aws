import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {
    id 'java'
    id 'maven-publish'
    id 'io.freefair.lombok' version '8.3'
    id 'jacoco'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'org.example'
version = '1.0-beta'

repositories {
    mavenCentral()
    maven { url "https://aws.oss.sonatype.org/content/repositories/snapshots" }
    // CodeGuru-Profiler java agent repository
    maven { url "https://d1osg35nybn3tt.cloudfront.net" }
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
            srcDir "$rootDir/../VideoAnalyticsVideoLogisticsJavaClient/generated-src"
        }
    }
    test {
        java {
            srcDir 'src/test/java'
        }
    }
}

dependencies {
    compileOnly 'com.google.code.findbugs:annotations:3.0.1'
    compileOnly 'com.google.code.findbugs:jsr305:3.0.1'

    // dependencies for OpenAPI generated java client
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
    implementation 'com.squareup.okio:okio:3.9.1'
    implementation 'io.gsonfire:gson-fire:1.9.0'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    implementation 'org.projectlombok:lombok:1.18.26'
    annotationProcessor 'org.projectlombok:lombok:1.18.26'

    implementation 'software.amazon.awssdk:dynamodb-enhanced:2.20.26'
    implementation 'software.amazon.cryptography:aws-database-encryption-sdk-dynamodb:3.1.0'
    implementation 'software.amazon.awssdk:iot:2.20.26'
    implementation 'software.amazon.awssdk:iotdataplane:2.20.26'
    implementation 'software.amazon.awssdk:s3:2.20.26'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation 'org.apache.logging.log4j:log4j-api:2.20.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.20.0'
    implementation 'com.amazonaws:aws-lambda-java-core:1.2.2'
    implementation 'com.amazonaws:aws-lambda-java-log4j2:1.5.1'
    implementation 'org.apache.logging.log4j:log4j-layout-template-json:2.20.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.7.1'
    implementation 'com.amazonaws:aws-lambda-java-events:3.11.1'

    implementation 'com.google.dagger:dagger-compiler:2.48'
    implementation 'com.google.auto:auto-common:1.2.1'
    implementation 'software.amazon.awssdk:url-connection-client:2.20.26'

    implementation 'com.github.wnameless.json:json-flattener:0.8.0'
    implementation 'com.github.fge:json-schema-validator:2.2.4'
    implementation 'com.networknt:json-schema-validator:1.5.4'


    implementation 'com.amazonaws:aws-encryption-sdk-java:2.4.0'
    implementation 'software.amazon.awssdk:kinesisvideo:2.29.6'
    implementation 'software.amazon.awssdk:kinesisvideosignaling:2.29.6'
    implementation 'software.amazon.awssdk:kinesisvideoarchivedmedia:2.29.6'
    implementation 'software.amazon.awssdk:kinesis:2.29.6'
    implementation 'commons-codec:commons-codec:1.17.1'
    implementation 'software.amazon.awssdk:apache-client:2.20.26'

    implementation 'org.slf4j:slf4j-api:1.7.36'
    implementation 'org.slf4j:slf4j-simple:1.7.36'
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.20.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.20.0'

    implementation 'org.opensearch.client:opensearch-rest-high-level-client:2.18.0'
    implementation 'io.github.acm19:aws-request-signing-apache-interceptor:3.0.0'

    // Test dependencies
    testImplementation 'software.amazon.awssdk:dynamodb-enhanced:2.20.26'
    testImplementation 'com.amazonaws:DynamoDBLocal:2.5.2'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.3'
    testImplementation 'com.github.stefanbirkner:system-rules:1.16.0'
    testImplementation 'org.mockito:mockito-core:5.11.0'
    testImplementation 'com.fasterxml.jackson.core:jackson-core:2.12.7'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.12.7.1'

    // Annotation processing
    annotationProcessor 'com.google.dagger:dagger-compiler:2.48'

    // Add Mockito instead
    testImplementation 'org.mockito:mockito-core:5.3.1'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.3.1'
    testImplementation 'org.mockito:mockito-inline:3.12.4'

    implementation 'javax.validation:validation-api:2.0.1.Final'
}

test {
    environment "JDK_JAVA_OPTIONS", "--add-opens java.base/java.util=ALL-UNNAMED"
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test

    // excluding classes generated by OpenAPI
     afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                "com/amazonaws/videoanalytics/videologistics/*.class",
                "com/amazonaws/videoanalytics/auth/*.class",
                "com/amazonaws/videoanalytics/*.class"
            ])
        }))
    }

    reports {
        xml.required = true
        xml.outputLocation = layout.buildDirectory.file('reports/jacocoXml/jacoco.xml')
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('reports/jacocoHtml')
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.5
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

task cleanBuildCache {
    doLast {
        delete layout.buildDirectory

        delete fileTree(dir: "${System.getProperty('user.home')}/.gradle/caches")
        delete fileTree(dir: "${System.getProperty('user.home')}/.gradle/daemon")
        delete fileTree(dir: "${System.getProperty('user.home')}/.gradle/wrapper")

        // Delete the .gradle directory in the project root, if it exists
        def projectGradleDir = new File(projectDir, '.gradle')
        if (projectGradleDir.exists()) {
            println "Deleting project's .gradle directory..."
            projectGradleDir.deleteDir()
            println "Project's .gradle directory deleted successfully."
        }

        println "Build cache cleaning completed."
    }
}

shadowJar {
    archiveClassifier.set('')
    mergeServiceFiles()
    zip64 true
    minimize() // This will remove unused classes
    // Exclude dependencies that are available in the Lambda runtime
    exclude 'org/apache/log4j/**'
    exclude 'org/apache/logging/log4j/**'
    exclude 'software/amazon/awssdk/**'
    outputs.upToDateWhen { false }
    dependsOn classes
}

/**
 * Copies the built JAR file to the lambda deployment assets directory.
 * The JAR file is copied from the build directory to a centralized assets location
 * that is used by the CDK deployment stack.
 *
 * This task depends on the shadowJar task to ensure the JAR is built before copying.
 * The task will always run regardless of whether inputs have changed.
 */
task transformJarToDeploymentAsset(type: Copy) {
    from "${buildDir}/libs/${rootProject.name}-${version}.jar"
    into "../../../assets/lambda-built/video-logistics-assets"
    outputs.upToDateWhen { false }
    dependsOn shadowJar
    
    doLast {
        println "JAR file copied from: ${buildDir}/libs/${rootProject.name}-${version}.jar"
        println "JAR file copied to: ../../../assets/lambda-built/video-logistics-assets"
    }
}

build {
    dependsOn shadowJar
    dependsOn classes
    finalizedBy transformJarToDeploymentAsset
}

